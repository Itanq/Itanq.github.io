
# 简要说明
这条EIP主要指定了一个顶级域名，注册器，以及使用ENS来反向解析以太坊地址的解析接口。这允许使用一个可读性高的名称来关联一个以太坊区块链的地址。解析器可以肯定的是反向记录是由以太坊地址的所有者发布的。

# 动机
虽然具名服务大多数情况多是用于正向解析 - 从便于人类可读的标识符到方便机器表示的符号 - 这里也有很多使用反向解析很有用的情况：
* 允许用户监控账户显示的名称的应用就明显要比监控账户的地址的应用要好一点，即使它最初是通过地址添加的。
* 把诸如描述性信息之类元数据添加到地址上以允许检索这个信息，而不用管这个地址最初是如何被发现的。
* 每个人都可以对一个地址配置一个名称，而不用关心地址的所有者。反向记录允许地址的所有者对这个地址宣称所有权。

# 规范
反向的ENS记录和常规的记录一样都存储在ENS层次结构中的一个保留域 `addr.reverse` 中.若要为一个给定账户的反向记录生成ENS名称，需将此账户转换小写的十六进制表示，并在它之后添加 `addr.reverse` 。比如，ENS 注册的地址是 `0x112234455c3a32fd11230c42e7bccd4a84e02010` 那么它的反向记录则存储在 `112234455c3a32fd11230c42e7bccd4a84e02010.addr.reverse` 。

注意，这意味若想要在合约中动态的反向解析地址，那么就需要在合约中执行十六进制的编码操作。

## Registrar
`addr.reverse` 域的所有者就是一个注册器，它允许调用者获取他们自己地址中的反向记录的所有权。它提供了如下几种方法：

### function claim(address owner) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，它指示 ENS 注册表转移一个名为 `hex(x) + 'addr.reverse'` 的所有权到参数提供的地址，并返回这个ENS记录转移的哈希值。

允许调用者指定一个所有者而不是他们自己（指被转移出去的记录），对于想要精确反向ENS记录的节点来说简化了合约（代码），将其委托给他们的创建者，同时他们的合约代码中只会有少量的代码：

```
reverseRegistrar.claim(msg.sender)
```

### function claimWithResolver(address owner, address resolver) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，它指示 ENS 注册表为名为 `hex(x) + '.addr.reverse'` （的地址）设置一个指定的解析器，然后转移它的所有权到参数提供的地址中，并返回这个 ENS 记录转移的哈希值。

这个方法对于需要设置自定义解析器和所有者的时候比直接调用`claim`方法需要更少的事务。

### function setName(string name) returns (bytes32 node)
当账户 `x` 调用这个函数的时候，就表示对名为 `hex(x) + '.addr.reverse'` （的地址）设置一个默认的解析器，并将该名称上的名称记录设置为指定的名称。这个方法对于用户需要在单个事务上设置一个简单的反向记录比较有用。

## Resolver interface
定义了一个新的解析器接口，包括下面的方法:

```
function name(bytes32 node) constant returns (string);
```

实现此接口的解析器必须为指定的节点返回一个合法的ENS名称，若指定的节点没有定义一个名称，则返回一个空字符串。

这个接口的接口 `ID` 是 `0x691f3431`。

未来的 EIPs 可能会。
指定更多适用于反向 ENS 记录的记录类型

# 附录1：注册器的实现
如下的注册器使用 Solidity 编写，实现了上面列出的规范。

```
pragma solidity ^0.4.10;

import "./AbstractENS.sol";

contract Resolver {
    function setName(bytes32 node, string name) public;
}

/**
 * @dev Provides a default implementation of a resolver for reverse records,
 * which permits only the owner to update it.
 */
contract DefaultReverseResolver is Resolver {
    AbstractENS public ens;
    mapping(bytes32=>string) public name;

    /**
     * @dev Constructor
     * @param ensAddr The address of the ENS registry.
     */
    function DefaultReverseResolver(AbstractENS ensAddr) {
        ens = ensAddr;
    }

    /**
     * @dev Only permits calls by the reverse registrar.
     * @param node The node permission is required for.
     */
    modifier owner_only(bytes32 node) {
        require(msg.sender == ens.owner(node));
        _;
    }

    /**
     * @dev Sets the name for a node.
     * @param node The node to update.
     * @param _name The name to set.
     */
    function setName(bytes32 node, string _name) public owner_only(node) {
        name[node] = _name;
    }
}

contract ReverseRegistrar {
    // namehash('addr.reverse')
    bytes32 constant ADDR_REVERSE_NODE = 0x91d1777781884d03a6757a803996e38de2a42967fb37eeaca72729271025a9e2;

    AbstractENS public ens;
    Resolver public defaultResolver;

    /**
     * @dev Constructor
     * @param ensAddr The address of the ENS registry.
     * @param resolverAddr The address of the default reverse resolver.
     */
    function ReverseRegistrar(AbstractENS ensAddr, Resolver resolverAddr) {
        ens = ensAddr;
        defaultResolver = resolverAddr;
    }

    /**
     * @dev Transfers ownership of the reverse ENS record associated with the
     *      calling account.
     * @param owner The address to set as the owner of the reverse record in ENS.
     * @return The ENS node hash of the reverse record.
     */
    function claim(address owner) returns (bytes32 node) {
        return claimWithResolver(owner, 0);
    }

    /**
     * @dev Transfers ownership of the reverse ENS record associated with the
     *      calling account.
     * @param owner The address to set as the owner of the reverse record in ENS.
     * @param resolver The address of the resolver to set; 0 to leave unchanged.
     * @return The ENS node hash of the reverse record.
     */
    function claimWithResolver(address owner, address resolver) returns (bytes32 node) {
        var label = sha3HexAddress(msg.sender);
        node = sha3(ADDR_REVERSE_NODE, label);
        var currentOwner = ens.owner(node);

        // Update the resolver if required
        if(resolver != 0 && resolver != ens.resolver(node)) {
            // Transfer the name to us first if it's not already
            if(currentOwner != address(this)) {
                ens.setSubnodeOwner(ADDR_REVERSE_NODE, label, this);
                currentOwner = address(this);
            }
            ens.setResolver(node, resolver);
        }

        // Update the owner if required
        if(currentOwner != owner) {
            ens.setSubnodeOwner(ADDR_REVERSE_NODE, label, owner);
        }

        return node;
    }

    /**
     * @dev Sets the `name()` record for the reverse ENS record associated with
     * the calling account. First updates the resolver to the default reverse
     * resolver if necessary.
     * @param name The name to set for this address.
     * @return The ENS node hash of the reverse record.
     */
    function setName(string name) returns (bytes32 node) {
        node = claimWithResolver(this, defaultResolver);
        defaultResolver.setName(node, name);
        return node;
    }

    /**
     * @dev Returns the node hash for a given account's reverse records.
     * @param addr The address to hash
     * @return The ENS node hash.
     */
    function node(address addr) constant returns (bytes32 ret) {
        return sha3(ADDR_REVERSE_NODE, sha3HexAddress(addr));
    }

    /**
     * @dev An optimised function to compute the sha3 of the lower-case
     *      hexadecimal representation of an Ethereum address.
     * @param addr The address to hash
     * @return The SHA3 hash of the lower-case hexadecimal encoding of the
     *         input address.
     */
    function sha3HexAddress(address addr) private returns (bytes32 ret) {
        addr; ret; // Stop warning us about unused variables
        assembly {
            let lookup := 0x3031323334353637383961626364656600000000000000000000000000000000
            let i := 40
        loop:
            i := sub(i, 1)
            mstore8(i, byte(and(addr, 0xf), lookup))
            addr := div(addr, 0x10)
            i := sub(i, 1)
            mstore8(i, byte(and(addr, 0xf), lookup))
            addr := div(addr, 0x10)
            jumpi(loop, i)
            ret := sha3(0, 40)
        }
    }
}
```

# 版权

Copyright and related rights waived via CC0.